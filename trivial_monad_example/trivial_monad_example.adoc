:description: F#/Ocaml monad example, railway oriented
:keywords: f#,ocaml,monadic error handling,monad,monoid
:stylesheet: readthedocs.css
:source-highlighter: pygments

I was reading through the various monad tutorials on the web but even after reading all these explanations
stuff wasn't really sinking in. What was the use of creating these monadic
types? It was all very abstract and the operations were defined on the abstractions,
and not on the concrete instance that was being manipulated. I'm not https://en.wikipedia.org/wiki/Alexander_Grothendieck[Grothendieck].
Apparently he worked on the generalisation first whereas most people spot patterns between specific instances and then generalise. Wild.

It kinda sunk in when I realized that the thing you put into a monad is a
chunk of code and that monads allow you to define operations on that chunk of
code in a sane manner. I also figured that to apply it I needed some practice
with them. So I concocted the example below. It's somewhat contrived but it
illustrates the problem that monads can solve.

The example below shows a typical flow of code that many programmers are
familiar with. Basically it's an 'if input is bad then fail else apply
function F on that something where F checks if input is bad then fail else apply
function H on input etc....' This scenario can lead to a sequence of nested 'if
... then' statements or in the example below nested 'match' statements. The nesting
is typically awkward to deal with. A monadic approach to error handling
allows it to be cleaned up.

Let's start with a basic scenario.
We have an account with some money and we want to debit from it multiple times.
In other words we have a sequence of transactions and we want to know if the entire process will fail or succeed.
_Ok, then just sum the transactions and if the result is less than or equal to the money in the account, then it succeeds._
That's fine, but what if we want to know which transaction fails, i.e. which transaction in the sequence will cause the account to drop below zero.
We can't just sum all the transactions because the information about the order of transactions will be squashed into the sum.
In an imperative OO language, assuming we have an object `acc` with method `transact`, we could come up with something like:

[source, c++]
----
if (acc.transact(-100)) {
  if (acc.transact(-200)) {
    if (acc.transact(-1000)) {
      cout << "Transaction succeeded" << endl;
    } else {
      cout << "Failed to transact 1000" << endl;
    }
  } else {
    cout << "Failed to transact 200" << endl;
  }
} else {
  cout << "Failed to transact 1000" << endl;
}
----

However this isn't clean and the nested `else` statements make it hard to read.
Moreover, it doesn't scale well at all.
Trying to add 10 more transactions will be like digging a grave with a spoon.
Then the 'lead architect' comes along and draws some diagrams, and writes a 50 page word document describing a bunch of classes, with names such as `AbstractFactoryInterface` and `TransactionInterface` and `TransactionErrorObserver`.
And after employing a project manager, engaging an outsourcing provider who outsource again because if it works once it will work twice, a few months and 10000 lines later we can say something like this:

[source, Java]
----
List<MetaTransaction> transactions = {Debit(100), Debit(200), Debit(1000)};
for (List<Transation>::Enumerator e = transactions.Begin(); e != transactions.End(); ++e)
  acc.transact(e)
System.Console.Interface.TheRealConsole(acc.status());
System.Console.WhitespaceSelectorInterface<Newline>.Print();
----

Forgive my Java, for I don't know that langauge.

That's a lot nicer on the surface and I can execute a list of transactions.
But it's a pig of thing underneath.
Overly abstracted classes and state, encapsulated within objects, that mutate variables that will need to be locked if concurrency requirements enter.

Let's go back to the original problem, the repeeated `if..else`.
What would a functional programmer do?
First thing to notice is the repeated pattern of `if .. else`.
It's kinda stupid to say notice it, because it's one of those things that, like your optical blind spot, is right infront of you.
It's so obvious and so ingrained that we don't notice it, especially if you've spent decades working with a language like Java, or any imperative language, that you miss the thing that is being repeated.
In this case it's the `if` statements.
Stuff that's repeated gets to be put in a loop or into a function or into a list for processing in a loop.
*Wouldn't it be nice if we could put an `if` statement into a function and pass it a parameter that is the function to apply on a successful test?*
Of course we can! We're functional programmers so statements in imperative langauges can be translated into expressions in a functional one.
Expressions are just values once evaluated, and values are immutable variables that can be passed around to other functions.
Those functions can be evaluated and passed around to other functions ad infinitum.

Let's switch to F# since it's easier to write this stuff in F#.
In the success case we need to apply the transaction.
So let's create a `transact` function and a type to record the result of any transaction.
In the failure case we record an error.

[source, fsharp]
----
type TransactResult =
  | Success of int // amount in account
  | Failure of string // amount in account at failure and an error message

let transact amount account =
  if account + amount >= 0 then
    Success (account + amount)
  else
    Failure ($"Failed to transact {amount} from {account}")
----

Now we need to chain multiple transactions together.
In mondadic terms this function is traditionally called `bind`, so we will reuse the name here.
We need to chain the return value of a transaction to the input of another transaction.
This means that both ends, the input and output, must have the same type.

[source, fsharp]
----
let bind (f : int -> TransactResult) (account : TransactResult) =
  match account with
  | Success amount ->
    f amount
  | Failure x -> Failure x
----

We can use bind as follows. The nested match statements are elided, and the error
handling path is hidden. Problem solved. You can try it for yourself in an F# interactive session.

[source,fsharp]
----
> let a = bind (transact 100) (Success 1000);;
val a : TransactResult = Success 1100

> let b = bind (transact -500) a;;
val b : TransactResult = Success 600

> let c = bind (transact -500) b;;
val c : TransactResult = Success 100

> let d = bind (transact -200) c;;
val d : TransactResult = Failure "Failed to transact -200 from 100"

> let d = bind (transact -200) d;;
val d : TransactResult = Failure "Failed to transact -200 from 100"

> let d = bind (transact -4300) d;;
val d : TransactResult = Failure "Failed to transact -200 from 100"

>
----


== Advanced

More advanced stuff can be done using compose `>>` to chain a sequence of transactions.

[source,fsharp]
----
// list of transaction functions
let transactions = [
  bind (transact 100)
  bind (transact -500)
  bind (transact -200)
  bind (transact -200)
  bind (transact -200)
  bind (transact -100)
]

// turn all of them into a single transaction function
let transactions' = List.reduce (>>) transactions

// apply the transactions to an initial account
transactions' (Success 1000)
----

The last parts might look weird if you're coming from an imperative background
and there's a lot going on there. Partial application (e.g. `transact 100`
where the last parameter is ommitted) and compose are common in functional
languages. Note that the `>>` works because the output type of `transact
100` is the same as the input. You might be tempted to expand these constructs
mechanically on paper and that might be instructive, but for day to day work
it's ponderous. It's easier to be fluent if it's bound to muscle memory. It's a
bit like writing a recursive functions. Once you get it, you get it.
Remember you can copy and paste this into `dotnet fsi` to test it out.

Now we've seen how you may come up with a monadic type from trying to solve the problem of
nested match functions. _But surely there is something in .NET for this?_ Yes, there is the https://docs.microsoft.com/en-us/dotnet/fsharp/language-reference/results[Result type] which provides support for monadic error handling.

== Summary

- You can add more transactions to the chain and be sure that the check on the balance being greater than zero will
always be executed.
- Note that this doesn't use mutable variables. There is no state. No writes are made to a variable `balance`.
Instead a new balance is calculated and passed as input to the next transaction in the chain.

== References

https://docs.microsoft.com/en-us/dotnet/fsharp/language-reference/results

Here's a better more detailed explanation than mine above:
https://swlaschin.gitbooks.io/fsharpforfunandprofit/content/posts/recipe-part2.html

And I think this is really good. It relates monoids to groups.
https://swlaschin.gitbooks.io/fsharpforfunandprofit/content/posts/monoids-without-tears.html

